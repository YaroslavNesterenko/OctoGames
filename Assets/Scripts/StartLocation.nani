; Location 1

@back bg_Scene
Hello! What is your name?

@input PlayerName summary:"Choose your name."
@stop

;1---------------------------------------------------------------------------------------------

@char Archibald scale:0.85 pos:35,0
Archibald: Hello {PlayerName}![i] Nice to see you. [i]I have an important task for you.
@wait 0.25
@questUpdate questName:"Quest received"

;2---------------------------------------------------------------------------------------------

Archibald: You should talk to Ben in Location2. [i]He knows how to help.
@wait 0.25
@char Archibald visible:false
@questUpdate questName:"Get information from Archibald"

;3---------------------------------------------------------------------------------------------
; Location 2

@back bg1
@char Ben scale:0.75 pos:75,0
Ben: Hello {PlayerName}![i] I heard that you were looking for me.
@wait 0.25
Ben: I have a mini-game for you. [i]Go through it and I will give you the information you need!
@wait 0.25

@hideAll
@startMiniGame wait:true

@back bg1
@char Ben scale:0.75 pos:75,0
Ben: You completed the mini-game! [i]Now I can help you.

@questUpdate questName:"Complete Ben's mini-game"

;4---------------------------------------------------------------------------------------------


Ben: Go to Location3 and pick up the item.
@char Ben visible:false

; Location 3
@back bg2
;@item "item_sprite"

@char Map scale:0.7 pos:50,35
{PlayerName}: This is the very item I need!
@wait 1
@char Map visible:false

@questUpdate questName:"Pick up item"

;5---------------------------------------------------------------------------------------------
; Location 2 (без НПЦ)

@back bg1

{PlayerName}: I'm here again... [i]But Ben is missing.
@questUpdate questName:"Talk to yourself"

;6---------------------------------------------------------------------------------------------
; Location 1

@back bg_Scene
@char Archibald visible:true
Archibald: Hello again, {PlayerName}! [i]Are you back?
@wait 0.25
@char Archibald tint:#808080
@char Ben visible:true
Ben: And what is your item?
@char Archibald tint:#ffffff
@char Ben tint:#808080
Archibald: Give me this item and I will help you with your further tasks.
@char Ben tint:#ffffff
@char Archibald tint:#808080
Ben: No, {PlayerName}! [i]Better give it to me! [i]I'm more needed.

@char Archibald tint:#ffffff
@char Ben tint:#ffffff

@hidePrinter

@choice "Give to Archibald" goto:.ChooseArchibald
@choice "Give to Ben" goto:.ChooseBen
@choice "Keep the item for yourself" goto:.ChooseYourself
@stop

;7---------------------------------------------------------------------------------------------

#ChooseArchibald
{PlayerName}: Okay, I'll give it to you, Archibald.
@wait 0.25

@char Archibald tint:#ffffff
@char Ben tint:#808080
Archibald: Thank you, {PlayerName}! I knew I could rely on you.
@wait 0.25

@char Ben tint:#ffffff
@char Archibald tint:#808080
Ben: How could you! I needed this!
@char Archibald tint:#ffffff
@char Ben tint:#ffffff
@wait 0.25

@goto .Final

;-----------------

#ChooseBen
{PlayerName}: This item is needed by Ben. [i]I'm giving it to you.
@wait 0.25

@char Ben tint:#ffffff
@char Archibald tint:#808080
Ben: Thank you! [i]I knew you would make the right choice.
@wait 0.25

@char Archibald tint:#ffffff
@char Ben tint:#808080
Archibald: You disappointed me, {PlayerName}!
@char Archibald tint:#ffffff
@char Ben tint:#ffffff
@wait 0.25

@goto .Final

;------------------

#ChooseYourself
{PlayerName}: No, I'll keep it!
@wait 0.25

@char Archibald tint:#ffffff
@char Ben tint:#808080
Archibald: This isn't fair, {PlayerName}! [i]You don't deserve trust.
@wait 0.25

@char Ben tint:#ffffff
@char Archibald tint:#808080
Ben: You're just selfish!
@char Archibald tint:#ffffff
@char Ben tint:#ffffff
@wait 0.25

@goto .Final

;------------------

#Final
@char Archibald visible:false
@char Ben visible:false

@questUpdate questName:"Final"
{PlayerName}: I completed the quest. [i]Well, life goes on…

@stop
;8---------------------------------------------------------------------------------------------
